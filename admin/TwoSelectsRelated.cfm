<cfsetting enablecfoutputonly="yes">

<!--- TAG PARAMETERS --->
<cfparam name="Attributes.Query">
<cfparam name="Attributes.Display1">
<cfparam name="Attributes.Display2">
<cfparam name="Attributes.Value1" default="#Attributes.Display1#">
<cfparam name="Attributes.Value2" default="#Attributes.Display2#">
<cfparam name="Attributes.Default1" default="(((((none)))))">
<cfparam name="Attributes.Default2" default="(((((none)))))">
<cfparam name="Attributes.id1" default="TwoSelectsRelated1">
<cfparam name="Attributes.id2" default="TwoSelectsRelated2">
<cfparam name="Attributes.Size1" default="1">
<cfparam name="Attributes.Size2" default="1">
<cfparam name="Attributes.Width1" default="">
<cfparam name="Attributes.Width2" default="">
<cfparam name="Attributes.ForceWidth1" default="">
<cfparam name="Attributes.ForceWidth2" default="">
<cfparam name="Attributes.ForceWidthChar" default="&nbsp;">
<cfparam name="Attributes.EmptyText1" default="">
<cfparam name="Attributes.EmptyText2" default="">
<cfparam name="Attributes.Message1" default="You must choose an option for #Attributes.id1#.">
<cfparam name="Attributes.Message2" default="You must choose an option for #Attributes.id2#.">
<cfparam name="Attributes.FormName" default="forms[0]">
<cfparam name="Attributes.htmlBetween" default="">
<cfparam name="Attributes.OnChange" default="">
<cfparam name="Attributes.ExtraOptions2" default="#IIF((Attributes.EmptyText1 EQ "") AND (Attributes.Size2 EQ 1), 0, 5)#">
<cfparam name="Attributes.AutoSelectFirst" default="yes">



<cfset FunctionName = ReplaceList(Attributes.FormName, "[,]", ",") & Attributes.id1 & "ChangeMenu()">


<!--- "MAGIC" SHORTCUTS FOR THE ONCHANGE HANDLER --->
<cfif Attributes.OnChange is "Jump!">
  <cfset Attributes.OnChange = "document.location = this.options[selectedIndex].value;">
<cfelseif Attributes.OnChange is "Submit!">
  <cfset Attributes.OnChange = "this.form.submit();">
</cfif>

<!--- USE PASSED QUERY WITHIN THIS CODE AS "MyQuery" --->
<cfset MyQuery = Evaluate("Caller.#Attributes.Query#")>

<!--- BEGIN JAVASCRIPTING --->
<cfsavecontent variable="js">
<cfoutput>
	<script type="text/javascript">
	/* <![CDATA[ */

		// javascript code generated by the CF_TwoSelectsRelated Cold Fusion tag (Nate Weiss, 4/00)
		// loosely adapted from Nick Heinle's code originally at http://webreference.com/javascript/960902/select_boxes.html
		// Modified to use prototype by Lois Chan 2008-11-21

		//var FirstArray = new Array;
		NewOpt = new Array;

		Event.observe(window, 'load', function() {
			$('#Attributes.id1#').observe('change', function() { #FunctionName# });
		});

		function #FunctionName# {
			menuNum = $F('#Attributes.id1#');

			switch(menuNum) {
		<!--- this function gets continued in the next cfoutput section --->
</cfoutput>


<!--- COUNTER VARIABLE WILL HOLD NUMBER OF GROUPS (OPTIONS IN FIRST SELECT) --->
<cfset Counter = IIF(Attributes.EmptyText1 is not "", 1, 0)>

<!--- CREATE AN "IF" STATEMENT THAT COVERS EACH ITEM IN THE FIRST SELECT BOX --->
<!--- WITHIN THE "IF" STATMENT, PRE-POPULATE ARRAY WITH CORRESPONDING ITEMS FOR SECOND SELECT  --->
<cfoutput QUERY="MyQuery" GROUP="#Attributes.Display1#">
				case '#CompanyID#':
					NewOpt = new Array;
					NewVal = new Array;
		<cfset Counter2 = IIF(Attributes.EmptyText2 is not "", 1, 0)>
		<cfif Attributes.EmptyText2 is not ""><cfoutput>NewOpt[0] = new Option("#Attributes.EmptyText2#", ""); </cfoutput></cfif>
		<cfoutput>NewOpt[#Counter2#] = new Option("#ReplaceList(MyQuery[Attributes.Display2][MyQuery.CurrentRow], "\,#Chr(9)#,#Chr(13)##Chr(10)#,',"",#Chr(13)#,#Chr(10)#",  "\\,\t,\n,\',\"",\r,\f")#", "#ReplaceList(MyQuery[Attributes.Value2][MyQuery.CurrentRow], "\,#Chr(9)#,#Chr(13)##Chr(10)#,',"",#Chr(13)#,#Chr(10)#",  "\\,\t,\n,\',\"",\r,\f")#"); <cfset Counter2 = Counter2 + 1></cfoutput>
					break;
		<cfset Counter = Counter + 1>
</cfoutput>


<!--- finish up the ChangeMenu() function --->
	<cfoutput>
				default:
					return;
			}

			tot = NewOpt.length;
			lst = $('#Attributes.FormName#').#Attributes.id2#.options.length;

			for (i = lst; i > 0; i--) {
			  $('#Attributes.FormName#').#Attributes.id2#.options[i] = null;
			}
			for (i = 0; i < tot; i++) {
			  $('#Attributes.FormName#').#Attributes.id2#.options[i] = NewOpt[i];
			}
			<cfif Attributes.AutoSelectFirst is "yes">
			  $('#Attributes.FormName#').#Attributes.id2#.options[0].selected = true;
			</cfif>
		}
	/* ]]> */
	</script>
</cfoutput>
</cfsavecontent>
<cfhtmlhead text="#js#" />
<!--- DONE WITH JAVASCRIPTING.  NOW WE JUST HAVE TO DISPLAY THE FORM ELEMENTS --->


<!--- ALLOW FOR AUTO-SIZING "SHORTCUT" OF SELECT BOXES --->
<cfif Attributes.Size1 is "Auto">
  <!--- MAKE THE FIRST SELECT BE BIG ENOUGH FOR ALL OF ITS OPTIONS --->
  <cfset Attributes.Size1 = Counter>
</cfif>
<cfif Attributes.Size2 is "Auto">
  <!--- MAKE THE SECOND SELECT BE THE SAME SIZE AS THE FIRST --->
  <cfset Attributes.Size2 = Attributes.Size1>
</cfif>



<!--- OUTPUT FIRST SELECT BOX --->
<cfoutput><select id="#Attributes.id1#" name="#Attributes.id1#" size="#Attributes.Size1#" <cfif Attributes.Width1 is not "">style="width:#Attributes.Width1#"</cfif>></cfoutput>

	<!--- SPECIAL FIRST ITEM, IF REQUESTED --->
	<cfif Attributes.EmptyText1 is not ""><cfoutput><option value="">#Attributes.EmptyText1#</option></cfoutput></cfif>
	<!--- GENERATE REMAINING ITEMS FROM QUERY --->
	<cfoutput QUERY="MyQuery" GROUP="#Attributes.Display1#"><option value="#MyQuery[Attributes.Value1][MyQuery.CurrentRow]#" <cfif MyQuery[Attributes.Value1][MyQuery.CurrentRow] is Attributes.Default1>selected="selected"</cfif>>#MyQuery[Attributes.Display1][MyQuery.CurrentRow]#</option></cfoutput>

	<!--- "FORCE WIDTH" OPTION AT BOTTOM, IF REQUESTED --->
	<cfif Attributes.ForceWidth1 is not ""><cfoutput><option value="">#RepeatString(Attributes.ForceWidthChar, Attributes.ForceWidth1)#</option></cfoutput></cfif>
<cfoutput></select></cfoutput>


<!--- INSERT ANY REQUESTED html BETWEEN THE TWO SELECT BOXES --->
<cfoutput>#Attributes.htmlBetween#</cfoutput>

<!--- OUTPUT SECOND SELECT BOX --->
<cfoutput><select id="#Attributes.id2#" name="#Attributes.id2#" size="#Attributes.Size2#" <cfif Attributes.onChange is not "">onChange="#Attributes.OnChange#"</cfif> <cfif Attributes.Width2 is not "">style="width:#Attributes.Width2#"</cfif>></cfoutput>
	<!--- SPECIAL FIRST ITEM, IF REQUESTED --->
	<cfif Attributes.EmptyText2 is not ""><cfoutput><option value="">#Attributes.EmptyText2#</option></cfoutput></cfif>

	<!--- GENERATE REMAINING ITEMS FROM QUERY --->
	<!--- WE ONLY NEED TO OUTPUT THE CHOICES FOR THE FIRST GROUP --->
	<cfif Attributes.EmptyText1 NEQ ""> <!--- Changed this line from IS to NEQ -- Elton --->
		<cfloop QUERY="MyQuery">
			<cfif MyQuery[Attributes.Value1][MyQuery.CurrentRow] is Attributes.Default1>
				<cfoutput><option value="#MyQuery[Attributes.Value2][MyQuery.CurrentRow]#" <cfif (MyQuery[Attributes.Value1][MyQuery.CurrentRow] is Attributes.Default1) AND (MyQuery[Attributes.Value2][MyQuery.CurrentRow] is Attributes.Default2)>selected="selected"</cfif>>#MyQuery[Attributes.Display2][MyQuery.CurrentRow]#</option></cfoutput>
			</cfif>
		</cfloop>
	</cfif>

  <cfif Val(Attributes.ExtraOptions2) GT 0>
    <cfloop FROM="1" TO="#Val(Attributes.ExtraOptions2)#" INDEX="i">
      <cfoutput><option value=""/></cfoutput>
    </cfloop>
  </cfif>

  <!--- "FORCE WIDTH" OPTION AT BOTTOM, IF REQUESTED --->
	<cfif Attributes.ForceWidth2 is not ""><cfoutput><option value="">#RepeatString(Attributes.ForceWidthChar, Attributes.ForceWidth2)#</cfoutput></cfif>
<cfoutput></select></cfoutput>


<cfsetting enablecfoutputonly="NO">
